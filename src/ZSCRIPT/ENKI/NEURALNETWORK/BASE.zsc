class Ek_NeuralNetwork{
    private
    uint Inputs;

    private
    Array<Ek_NeuralLayer> Layers;

    static
    Ek_NeuralNetwork Create(uint Inputs){
        Ek_NeuralNetwork Result=new('Ek_NeuralNetwork');
        Result.Inputs=Inputs;
        return Result;
    }

    bool Push(Ek_NeuralLayer Layer)
    {return Layers.Push(Layer);}

    Av_Matrix2 Get(Av_Matrix2 Input){
        for(int i=0; i<Layers.Size(); i++)
            Input=Layers[i].Get(Input);
        return Input;
    }

    void Train(Av_Matrix2 InputVals, Av_Matrix2 TargetVals){
        for(int i=0; i<InputVals.Height(); i++){
            Av_Matrix2 Input=InputVals.Row(i).ToM2(true),
                    Target=TargetVals.Row(i).ToM2(true);
            Array<Av_Matrix2> Inputs; Inputs.Clear();
            Inputs.Push(Input);
            for(int j=0; j<Layers.Size()-1; j++)
                Inputs.Push(Input=Layers[j].Get(Input));
            Av_Matrix2 Error=Target.Sub(Layers[Layers.Size()-1].Get(Input));
            for(int j=Layers.Size()-1; j>=0; j--)
                Error=Layers[j].Train(Inputs[j], Error, (j+1<Layers.Size())?Layers[j+1].Weights():null, j==Layers.Size()-1);
        }
    }
}
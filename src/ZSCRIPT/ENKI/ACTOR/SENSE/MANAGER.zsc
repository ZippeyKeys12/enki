enum Ek_EInfluence{
    INF_None,
    INF_Interest,
    INF_Danger,
}

class Ek_SenseManager:Av_Object{
    Ek_ContextMap Interest,
                  Danger;

    Array<Ek_Stimulus> Stimuli;

    static
    Ek_SenseManager Create(){
        let result=new('Ek_SenseManager');
        result.Interest=Ek_ContextMap.Create(.5);
        result.Danger=Ek_ContextMap.Create(.5);
        return result;
    }

    virtual
    void AddInfluence(Vector3 influence, Ek_EInfluence type){
        switch(type){
            case INF_Interest: return Interest.AddVector(influence);
            case INF_Danger: return Danger.AddVector(influence);
            default: Error("Invalid influence type: "..type);
        }
    }

    virtual
    void AddStimulus(Ek_Stimulus stim){
        if(Stimuli.Find(stim)!=Stimuli.Size())
            Error("Stimulus: "..stim.ID.." is Already Registered");
        Stimuli.Push(stim);
    }

    virtual
    Vector3 EvalDir(){
        Vector3 result=Interest.Evaluate()-Danger.Evaluate();
        if(result.Length())
            return result.Unit();
        else
            return result;
    }

    virtual
    void Age(){
        Interest.Age();
        Danger.Age();
        for(int i=0; i<Stimuli.Size(); i++){
            Ek_Stimulus stim=Stimuli[i];
            if(!stim){
                Stimuli.Delete(i);
            }if(stim.Decay==0){
                Stimuli.Delete(i);
                stim.Destroy();
            }else
                stim.Tick();
        }
    }

    virtual
    void Reset(){
        Interest.Reset();
        Danger.Reset();
        Stimuli.Clear();
    }
}